\ this treats the file as one long string and not line by line
: include" ( "path" -- )
  [char] " parse            ( -- addr len )
  r/o open-file throw       ( -- fd )
  dup file-size throw drop  ( -- fd len )
  2dup mmap throw           ( -- fd len addr )
  rot                       ( -- len addr fd )
  >r                        ( -- len addr )
  swap                      ( -- addr len )
  2dup 2>r                  ( -- )
  ['] evaluate catch        ( -- e-err )
  2r> munmap                ( -- e-err m-err ...save error )
  r> close-file             ( -- e-err m-err c-err ...save error )
  rot throw                 ( -- m-err c-err ...check evaluate error )
  throw throw ;             ( -- check others )
